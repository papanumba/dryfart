' mandelbrot.df: writes the set to a ppm file type P1
' algoriþm adapted from
' https://en.wikipedia.org/wiki/Plotting_algorithms_for_the_Mandelbrot_set
'   Unoptimized_naïve_escape_time_algorithm section

maxIter = 20.
width   = 300.
height  = 300.

!putColor!N%iter,!
    (iter < maxIter) => put!"1 ",.
    ()               => put!"0 ",.
    ().
!.

rwidth = R%width.
rheight= R%height.

put!"P1N$",.
put!width,.
put!" ",.
put!height,.
put!"N$",.

py = 0.
@(py < height) ' y pixels
    px = 0.
    @(px < width) ' x pixels
        x0 = -2.00 + R%px / rwidth  * 2.47. ' scaled x coord to -2.00, 0.47
        y0 = -1.12 + R%py / rheight * 2.24. ' y to -1.12, 1.12
        x = 0.0.
        y = 0.0.
        i = 0. ' iteration
        @(x*x + y*y < 4.0 & i < maxIter)
            xTmp = x*x - y*y + x0.
            y = 2.0*x*y + y0.
            x = xTmp.
            i ++ 1.
        @.
        putColor!i,.
        px ++ 1.
    @.
    put!"N$",.
    py ++ 1.
@.
