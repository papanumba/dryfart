use crate::asterix::*;
use std::str::FromStr;

grammar;


//match {
//    r"'.*" => {}, // comment
//    _
//}

pub Prog: Block = {
    Block
}

Block: Block = {
    <mut b:Block> <s:Stmt> => {b.push(s); b},
    Stmt => vec![<>],
}

Stmt: Stmt = {
    // variable assignement
    <id:Ident> "=" <e:Expr> "." =>
        Stmt::Assign(id.to_string(), *e),
    // "operation on" assignements
    <id:Ident> <op:r"[+\-*/&|]{2}"> <ex:Expr> "." =>
        Stmt::OperOn(id.to_string(), BinOpcode::from_str(&op[0..1]), *ex),
    // if else
    "(" <cond:Expr> ")" "=>" <b:Block>  <e:("(" ")" "=>" <Block>)?>
        "(" ")" "." =>
        Stmt::IfStmt(*cond, b, e),
    // while loop
    "@" "(" <cond:Expr> ")" <b:Block> "@" "." =>
        Stmt::LoopIf(*cond, b),
    // break
    "@" "(" ")" "." => Stmt::BreakL,
    // return
    "##" <Expr> "." => Stmt::Return(*<>),
    // procedure declaration
    "!" <name:Ident> "!" <pars:(<Type> <Ident> ",")*> "!"
    <body:Block> "!" "." =>
        Stmt::PcDecl(Proc::new(&name, &pars, &body)),
    // "return;" exit proc
    "!!" "." => Stmt::PcExit,
    // procedure call
    <name:Ident> "!" <args:(<Expr> ",")*> "!" "."
        => Stmt::PcCall(name, args),
}

Expr: Box<Expr> = {
    <AddExpr>,
    <BoolExpr>,
}

BoolExpr: Box<Expr> = {
    <l:BoolExpr> <o:r"[&|]"> <r:AtomExpr> =>
        Box::new(Expr::BinOp(l, BinOpcode::from_str(o), r)),
    <l:BoolExpr> <o:r"[&|]"> <r:BoolCmp> =>
        Box::new(Expr::BinOp(l, BinOpcode::from_str(o), r)),
    <l:AtomExpr> <o:r"[&|]"> <r:AtomExpr> =>
        Box::new(Expr::BinOp(l, BinOpcode::from_str(o), r)),
    <BoolCmp>,
}

BoolCmp: Box<Expr> = {
    <l:AddExpr> <o:r"(==|/=|<|>|<=|>=)"> <r:AddExpr> =>
        Box::new(Expr::BinOp(l, BinOpcode::from_str(o), r)),
}

AddExpr: Box<Expr> = { // FIXME: dunno y [+-] didnt work
    <l:AddExpr> <o:"+"> <r:MulExpr> => Box::new(Expr::BinOp(
        l, BinOpcode::from_str(o), r)),
    <l:AddExpr> <o:"-"> <r:MulExpr> => Box::new(Expr::BinOp(
        l, BinOpcode::from_str(o), r)),
    MulExpr,
}

MulExpr: Box<Expr> = {
    <l:MulExpr> <o:r"[*/]"> <r:Term> => Box::new(Expr::BinOp(
        l, BinOpcode::from_str(o), r)),
    Term,
}

Term: Box<Expr> = {
    AtomExpr,
    "(" "-" <t:Term> ")"  => Box::new(Expr::UniOp(
        t, UniOpcode::Sub)),
    PrimType AtomExpr => Box::new(Expr::Tcast(<>)),
};

AtomExpr: Box<Expr> = {
    Const => Box::new(Expr::Const(<>)),
    Ident => Box::new(Expr::Ident(<>.to_string())),
    "(" <Expr> ")",
    // anonymous function
    <rett:Type> "#" "{" <pars:(<Type> <Ident> ",")*> "}"
        <body:Block> "#" "." =>
            Box::new(Expr::Fdefn(Func::new(&pars, &body, &rett))),
    // function call
    <call:AtomExpr> "#" "{" <args:(<Expr> ",")*> "}" =>
        Box::new(Expr::Fcall(call,
            args.iter().map(|b| (**b).clone()).collect())),
    // explicit array
//    "{" <arr:(<Expr> ",")*> "}" => Box::new(Expr::Const(Val::B(false))),
}


Type: Type = {
    // funcs
    <r:Type> "#" "{" <a:(<Type> ",")*> "}" => Type::F(Box::new(r), a),
    // arrays
    "{" <Type> "}" => Type::A(Box::new(<>)),
    // BCNZR
    PrimType,
}

PrimType: Type = {
    r"[BCNZR]%" => Type::from_str(<>.get(0..1).unwrap()),
}

Ident: String = {
//    <i:ArrAccess> "_" AtomExpr => i,
    r"[A-Za-z][A-Za-z0-9]*" => <>.into(),
}

Const: Val = {
    "T" => Val::B(true),
    "F" => Val::B(false),
    r"[0-9]+"         => Val::N(u32::from_str(<>).unwrap()),
    r"[0-9]+\.[0-9]+" => Val::R(f32::from_str(<>).unwrap()),
    r#"C%".""# => Val::from_str_to_c(<>),
}
